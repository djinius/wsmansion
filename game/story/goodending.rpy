label goodEnding:
    # 배경: garden
    play music goodending

    scene garden
    show 엘리 눈썹놀람 눈사백안 입미소 눈물 음영있음:
        pos (.5, .05) anchor (.5, .0)
        zoom .5
    with dissolve

    독백 "나는 말없이 엘리를 안아주었다."
    독백 "엘리가 기쁨으로 몸을 파르르 떠는 것이 느껴졌다."

    # 엘리 등장
    # 엘리 (음영 on, 놀람, 사백안, 미소, 눈물)
    엘리 음영 눈썹놀람 눈사백안 입미소 눈물 "아, 아아아."

    # 엘리 (음영 on, 행복, 사백안, 큰 웃음, 눈물)
    엘리 눈썹행복 입큰웃음 "기뻐요, 너무 기뻐요. 너무 기쁘면 이렇게 숨이 막히기도 하는 걸까요."
    엘리 "상관없어요. 마침내 당신도 제 진심을 알아주신…."

    독백 "그리고 나는 엘리의 오른손을 들어 나의 가슴팍에 올렸다."

    # 엘리 (음영 on, 놀람, 사백안, 무표정)
    엘리 입무표정 눈물없음 "…."
    엘리 "이러지 마세요. 제발요."

    # 엘리 퇴장
    hide 엘리 with dissolve

    독백 "엘리의 가녀린 손가락이 내 손에 억지로 끌려 거미처럼 내 가슴을 지나다녔다."
    독백 "거미의 발걸음은 거미줄에서, 그것도 자신이 짜 놓은 거미줄에서 벗어나려는 듯 가냘펐다." 
    독백 "그렇게 억지로 끌려다니던 손가락 끝이 움푹 패인 곳을 디뎠다." 

    # 배경: garden
    독백 "이제야 또렷이, 무엇보다 완벽히 기억난다."
    독백 "마당에서 실랑이를 벌일 때 내가 느낀 통증은 머리뿐만이 아니었다."
    독백 "가슴이 너무나도 아팠다. 마치 무언가에 찔린 것처럼."
    독백 "그리고 나 이외에, 이 저택에서 날 찌를 수 있는 존재는 하나뿐이다."

    # 배경: knife
    scene knife with dissolve

    독백 "나를 일종의 지박령으로 만들어 자신처럼 저택에 묶어둘 심산인 존재."

    # 배경: garden
    # 엘리 등장
    # 엘리 (음영 on, 놀람, 사백안, 무표정)
    scene garden
    show 엘리 눈썹놀람 눈사백안 입무표정 음영있음:
        pos (.5, .05) anchor (.5, .0)
        zoom .5
    with dissolve

    엘리 "……."

    # 엘리 (놀람, 사백안, 미소)
    엘리 음영제거 입미소 "이해해요, 당신도 피부가 약했던 거군요."

    # 엘리 (음영 on, 놀람, 사백안, 미소, 눈물)
    엘리 눈물 음영있음 "분명 가증스러운 태양이 남긴 상처겠죠, 그렇죠?"

    # 엘리 (음영 on, 놀람, 사백안, 무표정, 눈물)
    엘리 입무표정 "제발 그렇다고 말해 줘요."
    엘리 "더 이상 절 그렇게 쳐다보지 말아 주세요. 부탁이에요. 전, 전 그저."

    독백 "아무 대답 없이 눈물을 흘리는 엘리를 가만히 지켜보았다."
    독백 "석양을 등진 채 서 있자니 가슴 속이 상쾌해졌다.\n넓은 들판을 자유로이 가로지르는 산들바람처럼 말이다."
    독백 "고독에 미쳐버린 유령에게 벌을 주고 싶은 마음보다\n모든 진실을 알고 난 뒤의 시원함이 더 컸다."
    독백 "그런데, 내 가슴속이 시원한 이유는 단지 그뿐만은 아닌 것 같았다."

    scene gardenScatter
    show 엘리 눈썹놀람 눈사백안 입무표정 음영있음:
        pos (.5, .05) anchor (.5, .0)
        zoom .5
    # with whiteNoiseSlow 

    # 엘리 (음영 on, 보통, 사백안, 무표정, 눈물)
    엘리 눈썹보통 "아, 안돼. 안 돼. 안 돼."

    독백 "구멍 뚫린 내 가슴에서 모래가 하염없이 흘러내리며,\n저만치서 불어온 바람을 타고 날아가기 시작했다."
    독백 "가루가 된 몸과 영혼이, 언젠가 내가 도달해야 할 곳으로 향하는 것이 느껴졌다."
    독백 "경악한 엘리가 날 막아세우려는듯 내 몸을 끌어안았지만,\n뒤집힌 모래시계처럼 계속 흘러내리는 모래를 막기에는 역부족이었다."
    독백 "한없이, 또 한없이 모든 것이 가벼워졌다."
   
    # 엘리 (보통, 사백안, 무표정, 눈물)
    엘리 눈웃음 입미소 눈물 "…잘못했어요, 제가 모두 잘못했어요."
    엘리 "지금까지 심하게 군 거 모두 사과드릴게요. 절 원망하셔도 상관없어요."

    # 엘리 (침울, 사백안, 무표정, 눈물)
    엘리 눈썹침울 "집에 잠시 다녀온다고 해도 굳게 믿고 보내드릴게요."
    엘리 눈사백안 "그러니까, 그러니까."

    # 엘리 퇴장
    # 배경: 살구색 NVL 모드
    nvl clear
    scene apricot with dissolve

    독백NVL "이윽고 지평선 아래로 지기 시작한 태양빛을 보며 생각했다."
    독백NVL "엘리와 나, 두 사람 모두 맑은 하늘 위를 노니는 한 쌍의 새였다면 좋았을 거라고."
    독백NVL "그렇게만 된다면 우리는 그 어떤 것에도 매이지 않은 채 함께 자유롭게 춤을 췄을 텐데."
    독백NVL "끝내 그렇게 되지 못한 것이 너무나도 아쉬웠다."
    독백NVL "누굴 탓해야 할까, 아무것도 모른 채 환히 내리쬐는, 저녁놀의 태양을?"

    # 배경: NVL 해제, garden => 모래로 사라져서 임시로 검은색 이미지를 넣음

    scene black
    show 엘리 음영없음 눈썹보통 눈실눈웃음1 입무표정 눈물실눈:
        pos (.5, .05) anchor (.5, .0)
        zoom .5
    with dissolve

    # 엘리 등장
    # 엘리 (보통, 실눈(실망), 미소, 눈물)
    엘리 눈썹보통 눈실눈웃음1 입미소 눈물실눈 "…어째서인지 이제는 화낼 힘도 없네요."
    엘리 "늘상 그래왔듯, 저택의 힘을 이용해서 당신을 멈출 수도 없어요."
    엘리 "어느 이기적이고 죄 많은 아이가 벌을 받아야만 하는 때가 온 걸까요."
    엘리 "아니면, 이것이 수많은 이들이 부르짖어왔던 ‘운명’이라는 걸까요."

    # 엘리 (행복, 웃음, 미소, 눈물)
    엘리 눈썹행복 눈웃음 "언젠가 당신이 저에게서 벗어나 자유로워지는 것, 그걸 받아들이는 것까지."
    엘리 "그것이야말로 제가 계속 부정해왔던 저의 운명이자 과업이었군요."
    엘리 "계속, 끊임없이 저에게서 벗어나려고 하는 당신이 그렇게나 야속했는데."

    # 엘리 (놀람, 웃음(실눈), 미소, 눈물)
    엘리 눈썹놀람 눈실눈웃음1 "그런데 당신은, 당신의 따스한 미소는,\n왜 마지막에 와서야 이렇게 아름다운 건가요."

    독백 "엘리를 향한 배신감, 모멸감은 모두 눈 녹듯 사라지고, 내 앞에서 하염없이 우는 소녀를 위로하지도, 안아주지도 못하는 지금이 안타깝기만 했다."
    독백 "돌봐줄 사람 없이 홀로 남은 어린아이의 울음소리가 더욱 커졌다."

    # 배경: sky
    scene sky with dissolve

    # 엘리 (행복, 웃음(실눈), 미소, 눈물)
    엘리 눈썹행복 "지금까지 떼쓴 거 미안해요, 그리고 고마워요."
    엘리 "그러니 염치없지만, 마지막으로 하나만 알려주세요."
    엘리 "제게 주어진 짐을 짊어질 수 있게,\n언제까지고 당신과의 시간을 기억할 수 있게……."

    독백 "그것이 내가 온전히 사라지기 전, 엘리가 내게 건넨 마지막 질문이었다."

    stop music
    play sound "audio/sfx/whoosh.mp3" noloop

    # NVL, 텍스트 입력 창 등장 : 
label inputName:
    if not _in_replay:
        $ myName = renpy.input("“당신의 이름을 알려주세요.”", length=5)
        if myName == "":
            jump inputName

        $ persistent.myName = myName

    # 플레이어가 이름을 입력한 뒤 타이틀 화면으로 복귀, 타이틀 CG에 엘리 대신 파랗고 쾌청한 하늘이 나타남
    $ persistent.skyBackground = True

    return
