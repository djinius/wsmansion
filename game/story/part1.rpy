label part1:

    scene black
    show text "응접실":
        align (.0, .0)
    show 아비게일처음:
        pos (.5, .05) anchor (.5, .0)
        zoom .5
    with dissolve

    나 "……."
    나 "푹신한 소파에 앉아 주위를 둘러본다."
    나 "아무리 생각해도 의문이다. 당장 이 저택부터 그렇다."
    나 "바깥부터 내부까지, 고풍스러운 걸 넘어 과거의 유물 같은 곳이다."
    나 "가구는 물론 전등에 액자까지, 웬만한 골동품 가게에서도 보기 힘든 물건들로 빼곡하다."
    나 "그런데도 먼지 몇 톨 떠다니는 걸 빼면 깔끔하다 못해 쾌적함이 느껴진다."
    나 "여기 주인은 대체 뭐 하는 사람이고, 날 맞아준 저 소녀와는 무슨 관계일까?"

    # 소녀? (음영 off, 보통, 웃음, 미소, 눈물 x)
    아비게일처음 "많이 기다리셨죠, 손님 응대는 정말 오랜만이라서요."

    나 "소녀가 찻잔에 음료를 따라 가져왔다."
    나 "목이 말랐던 나는 입을 데지 않게 내용물을 천천히, 부드럽게 들이켰다."
    나 "그러자 내 목구멍을 때리는, 홍차답지 않은 시원한 감각."
    나 "달다 못해 아려오는 혀끝에 솨- 하고 올라오는 탄산까지."
    나 "살면서 콜라를 찻잔에 담아 오는 사람은 처음 봤다. "
    나 "일부러 날 골탕 먹일 셈이었던 건지, 연신 기침을 하는 날 보며 입을 가리고선 웃음을 참는 소녀."
    # 소녀? (음영 off, 난처, 실눈 웃음, 큰 웃음, 눈물 x)
    아비게일처음 웃음 "푸후훗, 아, 죄송, 죄송해요. 그렇게까지 반응이 격할 줄은 몰라서."

    나 "부잣집 아가씨치고는 참으로 맹랑한 태도에 헛웃음이 났다."
    나 "초면이지만 어째서인지 미워할 수는 없었다. 귀여워서 그런가."
    나 "그리고 이제 다시 보니, 소녀의 옷도 예스럽기 그지없다."

    # ( 소녀(엘리)의 scg 확대 - '옷 부분 위주로' )
    show 아비게일처음:
        linear .5  zoom 1.
    pause 1.

    나 "언뜻 수수해 보이는 원피스를 바탕으로 적당한 포인트를 주었는데, 고전 흑백 영화에서나 나올 법한 비주얼에도 전혀 촌스럽지 않았다."
    나 "어쩌면 이것이 상류층의 기품이란 걸지도."
    나 "소녀는 잔 속의 콜라를 조심히 홀짝이고선 입을 열었다."

    show 아비게일처음:
        linear .5  zoom .5
    pause 1.

    # 소녀? (음영 off, 보통, 웃음, 무표정, 눈물 x, scg 다시 축소)
    아비게일처음 보통 "그럼, 실례가 아니라면 여기엔 무슨 용건이신지 설명을 부탁드려도 될까요."
    나 "…대답할 길이 없었다."
    나 "여기에 어떻게 왔는지, 심지어 내가 누군지 조차 모른다. 너무 단순해서 설명할 수 없는 처지를 과연 누가 믿어주겠는가."
    # 소녀? (음영 off, 보통, 웃음, 미소, 눈물 x)
    # 소녀? (음영 off, 보통, 웃음, 미소, 눈물 x)
    아비게일처음 미소 "음…무언가 말로는 못 할 사정이 있으신 것 같은데, 맞나요?"
    나 "그러자, 소녀는 내게 의심의 눈초리 대신 따스한 미소를 건네주었다."
    나 "고개를 끄덕였다. 소녀의 미소가 더욱 밝아졌다."
    # 소녀? (음영 off, 행복, 실눈 웃음, 큰 웃음, 눈물 x)
    아비게일처음 웃음 "후훗, 그래도 너무 걱정하지는 마세요. "
    아비게일처음 "여기가 산골 부근이라, 길을 잃은 등산객분들께서 가끔 찾아오시거든요."
 
    # 소녀? (음영 off, 침울, 슬픔, 무표정, 눈물 x)
    아비게일처음 무표정 "오히려 도와드릴 게 없어 죄송합니다."
    아비게일처음 "보시다시피, 가구는 물론 전화기도 모두 이렇다 보니 바깥과 연락할 수단도 시원찮고, 비상 연락망도 마침 고장이라…. 면목 없네요."

    나 "나는 괜찮다는 의미로 고개를 저으며 멋쩍은 미소를 지었다."
    나 "의심받아도 모자랄 상황에 환대라니, 이렇게 좋을 수가…."
    나 "…순간적으로 스치는 미심쩍은 느낌에 나는 다시 자리를 고쳐 앉았다."

    # 소녀? (음영 off, 난처, 놀람, 무표정, 눈물 x)
    아비게일처음 궁금 "무슨 일이시죠? 어디 다치기라도 하신 건가요?"
    
    # scg off
    나 "예로부터 모르는 사람의 이유 없는 환대에는 대부분 구린 이유가 있다."
    나 "내가 기억을 잃었고, 그 이유조차 모르는 상황에서는 더더욱."
    나 "지금 비상 연락망이 끊어진 건 그렇다 치자. 그럼 왜 먼저 그 이야기를 꺼냈을까? 전화기를 잃어버렸다는 걸 미리 알고 있었나?"
    나 "또 '말 못할 상황'인건 어떻게 알고 있었지?"
    # 소녀? (음영 off, 행복, 실눈 웃음, 미소, 눈물 x)
    아비게일처음 웃음 "……우후훗."
    # 소녀? (음영 on, 행복, 실눈 웃음, 미소, 눈물 x)
    아비게일처음 미소 "결국 들켜버렸나요. 그럼 자, 순순히 이리로 오세요…."

    나 "소녀의 삐죽하고 날카로운 덧니가 반짝이고, 빈 찻잔과 소녀의 모습이 눈앞에 아른거렸다."
    # 배경: 검은 화면

    scene black
    나 "이런 상황일수록 조심했어야 했는데,"

    # 소녀? 
    아비게일처음 입벌림 "크왕!"
    scene black
    show text "응접실":
        align (.0, .0)
    show 아비게일처음 입벌림:
        pos (.5, .05) anchor (.5, .0)
        zoom .5
    with dissolve

    # 소녀? (음영 off, 놀람, 실눈 웃음, 큰 웃음, 눈물 x)
    나 "순간적으로 놀라 움츠러든 내 앞에 소녀가 크게 입을 벌리고 있었다."
    나 "양팔을 벌리고, 모든 손가락을 맹수처럼 세우고서 말이다."
    # 소녀? (음영 off, 놀람, 놀람, 무표정, 눈물 x)
    아비게일처음 궁금 "예? 어라… 저만 그 생각 한 건가요?"
    # 소녀? (음영 off, 행복, 웃음, 무표정, 눈물 x)
    아비게일처음 미소 "흐음, 수상한 대저택에 홀로 사는 창백한 주인아씨에, 튀어나온 송곳니까지."
    # 소녀? (음영 off, 행복, 실눈 웃음, 큰 웃음, 눈물 x)
    아비게일처음 입벌림 "상황이 딱 봐도 흡혈귀잖아요, 흡혈귀. 크와앙!"

    show 아비게일처음 보통:
        linear .5 ypos .05 zoom .5
    pause 1.

    나 "나는 멋쩍게 웃어 보이는 걸로 대답을 대신했다."
    나 "전혀 예상하지는 못했지만, 썩 귀여운 장난이었다."
    나 "부끄러워 얼굴을 붉히는 기색도 없이 다시 신비로운 분위기로 돌아간 걸로 보아, 콜라도 그렇고 제 나이대에 맞게 장난을 좋아하는 편인가, 생각했다."

    # 엘리 (음영 off, 행복, 웃음, 미소, 눈물 x)
    아비게일처음 "그럼 다시, 지금까지의 실례를 무릅쓰고, 처음 뵙겠습니다."
    hide 아비게일처음
    show 아비게일 보통:
        pos (.5, .05) anchor (.5, .0)
        zoom .5
    아비게일 "제 이름은 아비게일 F. 엘리엇."
    아비게일 "잠시 요양 중인 엘리엇 가의 차녀랍니다. 편하게 줄여서 ‘아비’라고 불러주세요."

    나 "……."
    # 엘리 (음영 off, 난처, 실눈 웃음, 미소, 눈물 x)
    hide 아비게일
    show 엘리:
        pos (.5, .05) anchor (.5, .0)
        zoom .5
    엘리 "…역시 그건 좀 아니겠지요? 음, 엘리엇에서 따서, ‘엘리’가 좋겠어요."

    나 "두 음절짜리 단어의 울림이 신기한 듯, 소녀는 몇 번이고 오물거렸다."

    #엘리 (음영 off, 행복, 웃음, 미소, 눈물 x)
    엘리 "엘리, 엘리. {i}엘리{/i}…. 후훗. 역시 마음에 들어요."
    엘리 "그럼 머무시는 동안이라도 집처럼 편하게 계세요."
    엘리 "아, 무슨 일 있으시면 저 부르시는 거, 잊지 마세요?"

    나 "소녀, 아니 엘리는, 가볍게 고개를 숙인 뒤 종종걸음으로 사라졌다."
    나 "자기소개도 끝마친 참이지만, 사실 미심쩍은 느낌이 모두 가신 건 아니었다."
    나 "갈피가 잡히지 않는 상황, 조각상, 오래된 저택, 그리고 엘리…."
    나 "무언가 실마리가 보일 듯하면서도 연결되지 않는다. 기억의 빈자리가 크다."
    나 "날 도와줄 사람은 올 기미가 없고, 엘리를 100\% 신뢰하지 못하는 지금, 난 오로지 내 힘으로 이 저택에 얽힌 비밀을 풀어 집으로 돌아가야만 한다."
    나 "감시당하고 있을 수 있으니 조심히 움직이자. 나는 먼저……."

    menu:
        "탁자 위를 조사한다":
            call investigateTable
        "냉장고 안을 조사한다":
            call investigateRefrigerator
        "찬장을 조사한다":
            call investigateCupboard

    show text "마우스 스크롤 버튼으로 대사록을 열어 선택지 이전으로 돌아갈 수 있습니다." at truecenter with dissolve
    pause
    hide text with dissolve

    나 "엘리와 별 시덥잖은 이야기만 나눈 것 같았지만, 소득이 아예 없는 것도 아니었다."
    나 "저택 이곳저곳을 지날 때마다 어렴풋이 무언가 떠오르는 것 같은 느낌을 받았다."
    나 "그 장소들을 조사하다 보면 내 기억을 되찾으리라는 강한 확신이 들었다."
    나 "다시 엘리가 떠난 걸 확인했으니, 이번엔 무언가, 무언가 단서를 찾아야만 한다."

    return

label investigateTable:
    # 배경: 응접실

    scene black
    show text "응접실":
        align (.0, .0)
    with dissolve

    나 "처음부터 너무 앞서 나갈 필요는 없다. 가까운 곳부터 살펴보자."

    # cg: 두꺼운 책
    나 "책상 위에 두꺼운 영어책이 놓여있다. 아가씨답게 고상한 취미다."
    나 "아무 페이지나 펼쳐 보았다, 으극, 역시 영어라 그런지 읽질 못하겠다."
    나 "그런데 내가 펼친 곳에 무언가 껴 있다. 드라이 플라워인가?"

    # 엘리 (음영 off, 행복, 실눈 웃음, 미소, 눈물 x)
    엘리 웃음 "문빔으로 만들었어요! 정말 예쁘죠?"
    나 "엘리에게 문빔이 무엇인지 물었다."
    # 엘리 (음영 off, 행복, 웃음, 미소, 눈물 x)
    엘리 보통 "아, 이 꽃 이름이에요. 달처럼 은은하게 빛난다고 해서 문빔이랍니다."
    엘리 "안뜰에 핀 걸 한 송이 따서 책갈피로 만들어 봤어요. 예쁘죠?"
    나 "확실히 예쁜 꽃이다. 그런데 어째 안뜰과는 달리 저택 내부에선 화분 하나 본 적이 없다."

    # 엘리 (음영 off, 행복, 웃음, 미소, 눈물 x)
    엘리 미소 "생각해보니 혼자서 심심하셨겠어요, 서재에서 뭐라도 읽으실래요?"
    나 "나는 정중히 사양했다."

    return

label investigateRefrigerator:
    # 배경: 주방
    scene black
    show text "주방":
        align (.0, .0)
    with dissolve

    나 "문득 엘리의 장난이 떠오른다. 흡혈귀, 흡혈귀라."
    나 "영화 같은 데서도, 이런 식으로 자신의 정보를 은연중에 흘리지 않던가?"
    나 "나는 높은 확률로 엘리의 ‘희생자’들이 보관되어 있을 곳을 향했다."
    나 "식은땀이 흐르는 손으로 오래된 냉장고 문을 열자 드러난 것은 {color=F00}새빨간{/color}{nw}{w=2.5}"
    extend " 콜라 라벨이 붙은 유리병들이었다! 그것도 아주 많이."
    나 "이놈의 흡혈귀는 피가 아니라 콜라를 먹고 사나, 싶을 정도로 콜라병들이 냉장고 안을 빽빽이 채우고 있었다."
    나 "서늘한 기운이 감돌았다. 냉장고 탓인가."
    # 엘리 (음영 on, 난처, 실눈 웃음, 미소, 눈물 x)
    엘리 난처 "……지금 뭘 하고 계실까요?"
    엘리 "남의 집 냉장고를 함부로 뒤지면 안 된다고, 배우지 않으셨을까요?"

    나 "…아무래도 냉장고가 아니라, 화가 난 엘리 때문인 것 같았다."
 
    return

label investigateCupboard:

    나 "무턱대고 발걸음을 옮기긴 했으나, 어디로 가야 할지 갈피가 안 잡힌다."
    나 "어느새 주방의 찬장 앞에 도달하긴 했는데, 들어있는 건, 풍선껌?"
    나 "엘리의 눈치가 보이긴 했지만, 껌 하나를 입에 넣고 부드러워질 때까지 질겅질겅 씹었다."
    나 "그리고 풍선을 불려고 했는데… 생각보다 잘 되지 않는다."
    # 엘리 (음영 off, 보통, 웃음, 무표정, 눈물 x)
    나 "언제 내 곁에 왔는지 모를, 풍선을 잘만 만드는 엘리와는 다르게 말이다."
    나 "엘리가 내게 웃으며 풍선껌을 건넸다."
    # 엘리 (음영 off, 보통, 웃음, 미소, 눈물 x)
    엘리 미소 "여기, 하나 더 시도해 보시겠어요?"
    나 "풍선껌을 받아 입에 넣고서, 질겅, 질겅. 그리고 바람을 불면…."
    나 "…또 실패했다. 엘리는 잘만 하는데."
    나 "그렇게 우리는 찬장 속 풍선껌이 다 떨어질 때까지 껌을 씹고, 또 씹었다."

    return
